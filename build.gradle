plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

group 'com.github.romanqed'
archivesBaseName = 'ranges'
version = System.getenv('RELEASE_VERSION') ?: "0.0.1"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

test {
    useJUnitPlatform()
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allJava
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId group
            artifactId archivesBaseName
            version version

            from components.java
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv('SONATYPE_USERNAME')
                password System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        root.children().last() + {
            resolveStrategy = DELEGATE_FIRST

            description 'A small java library that implements ranges'
            name 'ranges'
            url 'https://github.com/RomanQed/ranges'
            organization {
                name 'com.github.romanqed'
                url 'https://github.com/RomanQed/ranges'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/RomanQed/ranges/issues'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/RomanQed/ranges/blob/main/LICENSE'
                }
            }
            scm {
                url 'https://github.com/RomanQed/ranges'
                connection 'scm:https://github.com/RomanQed/ranges.git'
                developerConnection 'scm:git://github.com/RomanQed/ranges.git'
            }
            developers {
                developer {
                    id 'dev'
                    name 'RomanQed'
                    email 'gbakaldin@gmail.com'
                }
            }
        }
    }
}